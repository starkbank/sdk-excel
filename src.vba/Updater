

Public Function EcdsaRoutine()
    Dim fileName As String
    Dim sheet As Worksheet
    Dim total As Integer
    Dim fd As Office.FileDialog
    
    
    EcdsaRoutine = False
    
    If isEcdsaAvailable() Then
        EcdsaRoutine = True
        Exit Function
    End If
    
    ViewEcdsaForm.Show
    Set fd = Application.FileDialog(msoFileDialogFilePicker)

    fd.AllowMultiSelect = False
    fd.Title = "Por Favor selecione o arquivo baixado: ""StarkBank Embedded.zip""."
    fd.Filters.Clear
    
    If fd.Show = False Then
        MsgBox "Por Favor selecione o arquivo baixado: ""StarkBank Embedded.zip""."
        Exit Function
    End If
    fileFullPath = fd.SelectedItems(1)
    fileName = Dir(fileFullPath)
    filepath = Replace(fileFullPath, fileName, "")
    
    If UnzipFile(fileFullPath, filepath) = False Then
        MsgBox "Por Favor selecione o arquivo baixado: ""StarkBank Embedded.zip""."
        Exit Function
    End If
    
    destinationFileName = ActiveWorkbook.path + "\SBEmbedded-Test" ' TODO fix the file name after done
    
    pathUnzip = Replace(fileFullPath, ".zip", "")
    If Dir(pathUnzip, vbDirectory) = "" Then
        pathUnzip = filepath + "SBEmbedded-Test" ' try original name
        If Dir(pathUnzip + "\", vbDirectory) = "" Then
             MsgBox "Módulo de segurança não encontrado, faça o download novamente."
             Exit Function
        End If
    End If
    
    Call moveFile(pathUnzip, destinationFileName)
    
    If isEcdsaAvailable() Then
        MsgBox "Instalação bem sucedida!"
    End If
    
    EcdsaRoutine = True
    
End Function

Function UnzipFile(zippedFileFullName As Variant, unzipToPath As Variant)
    Dim ShellApp As Object
    
    
    UnzipFile = False
    If InStr(zippedFileFullName, ".zip") <> 0 Then
        Set ShellApp = CreateObject("Shell.Application")
        ShellApp.Namespace(unzipToPath).CopyHere ShellApp.Namespace(zippedFileFullName).Items
        UnzipFile = True
    End If
    
End Function

Public Function isEcdsaAvailable()
    Dim path As String: path = ActiveWorkbook.path
    ecdsaFileName = "\SBEmbedded-Test" ' TODO fix the file name after done
    
    executable = Dir(path + ecdsaFileName + "\Starkbank-Utils.exe") ' TODO fix the file name after done
    dll = Dir(path + ecdsaFileName + "\Starkbank-Utils.dll")
    depsJson = Dir(path + ecdsaFileName + "\Starkbank-Utils.deps.json")
    configJson = Dir(path + ecdsaFileName + "\Starkbank-Utils.runtimeconfig.json")
    
    result = True
    If executable = "" Or dll = "" Or depsJson = "" Or configJson = "" Then
        result = False
    End If
    
    isEcdsaAvailable = result
    
End Function

Public Function moveFile(sourceFilePath, detinationPath)
    Dim FSO As Object
    Dim ecdsaFileName As String: ecdsaFileName = "\SBEmbedded-Test" ' TODO fix the file name after done
    
    
    On Error GoTo eh:
    Set FSO = CreateObject("Scripting.Filesystemobject")
    
    ecdsaFilePath = ActiveWorkbook.path + ecdsaFileName
    If Dir(ecdsaFilePath + "\", vbDirectory) <> "" Then
        FSO.DeleteFolder ecdsaFilePath, True
    End If
    
    FSO.MoveFolder Source:=sourceFilePath, Destination:=detinationPath
    Exit Function
eh:
    MsgBox "Faça o download do módulo StarkBank-Utils novamente"
    Exit Function
End Function

Public Sub UpdateRoutine()
    Dim currentVersion As String
    Dim repoVersion As String
    Dim updateQuestion As String
    Dim updateAvailable As Boolean
    
    
    On Error GoTo Err
    currentVersion = "v" + getCurrentVersion()
    repoVersion = getLatestRepoVersion()
    
    Debug.Print "Current:", currentVersion
    Debug.Print "Remote:", repoVersion
    
    updateAvailable = SemVerCompare(currentVersion, repoVersion)
    
    If Not updateAvailable Then
        Exit Sub
    End If
    
    If AskUpdate(repoVersion) = vbNo Then
        MsgBox "Atualização cancelada."
        Exit Sub
    End If
    
    If RenameCurrent() = vbYes Then
        Update (repoVersion)
    Else
        MsgBox "Atualização cancelada."
    End If
    
    Exit Sub
Err:
    MsgBox "Não foi possível verificar atualizações. Favor checar a conexão."
    Exit Sub
    
End Sub

Public Function UpdateVersionNumber(newVersion As String)
    Sheets("Credentials").Cells(9, 2) = newVersion
    Sheets("Principal").Cells(1, 7) = "SDK Stark Bank - " & newVersion
    
End Function

Public Function SemVerCompare(currentVersion As String, repoVersion As String)
    Dim allMatches1 As Object
    Dim allMatches2 As Object
    Dim exp As String
    
    Dim currentMajor As Long
    Dim currentMinor As Long
    Dim currentPatch As Long
    
    Dim repoMajor As Long
    Dim repoMinor As Long
    Dim repoPatch As Long
    
    Dim majorBool As Boolean
    Dim minorBool As Boolean
    Dim patchBool As Boolean
    
    
    On Error Resume Next

    exp = "(v?)(0|[1-9]\d*)\.(0|[1-9]\d*)\.(0|[1-9]\d*)(?:-((?:0|[1-9]\d*|\d*[a-zA-Z-][0-9a-zA-Z-]*)(?:\.(?:0|[1-9]\d*|\d*[a-zA-Z-][0-9a-zA-Z-]*))*))?(?:\+([0-9a-zA-Z-]+(?:\.[0-9a-zA-Z-]+)*))?$"
    With CreateObject("VBScript.RegExp")
        .Pattern = exp
        .Global = True
        .IgnoreCase = True
        Set allSubMatches1 = .Execute(currentVersion)(0).submatches
        Set allSubMatches2 = .Execute(repoVersion)(0).submatches
    End With
    
    currentMajor = CLng(allSubMatches1(1))
    currentMinor = CLng(allSubMatches1(2))
    currentPatch = CLng(allSubMatches1(3))
    
    repoMajor = CLng(allSubMatches2(1))
    repoMinor = CLng(allSubMatches2(2))
    repoPatch = CLng(allSubMatches2(3))
    
    majorBool = (repoMajor > currentMajor)
    minorBool = (repoMajor = currentMajor) And (repoMinor > currentMinor)
    patchBool = (repoMajor = currentMajor) And (repoMinor = currentMinor) And (repoPatch > currentPatch)
    
    SemVerCompare = False
    
    If majorBool Or minorBool Or patchBool Then
        SemVerCompare = True
    End If
    
End Function

Public Sub Update(repoVersion As String)
    Dim successMessage As String
    Dim overwriteLatest As Boolean
    
    
    DownloadLatest
    OpenDownloaded
    CloseCurrent
    UpdateVersionNumber (repoVersion)
    
End Sub

Public Function AskUpdate(repoVersion As String)
    updateQuestion1 = "Versão nova disponível para atualização: " + repoVersion + vbNewLine
    updateQuestion2 = "Atualizar agora? Você precisará transferir manualmente seus dados para a nova versão!"
    AskUpdate = MsgBox(updateQuestion1 + updateQuestion2, vbYesNo)
    
End Function

Private Function RenameCurrent()
    Dim FSO As New Scripting.FileSystemObject
    Dim baseName As String
    Dim newName As String
    Dim oldName As String
    Dim overwriteLatest As Integer
    Dim overwriteMessage As String
    
    
    oldName = ThisWorkbook.FullName
    
    baseName = FSO.GetBaseName(ThisWorkbook.name)
    overwriteLatest = vbYes
    Debug.Print Dir(ThisWorkbook.path & "/starkbank-sdk.xlsm")
    If baseName = "starkbank-sdk" Then
        newName = baseName + "_OLD.xlsm"
        ThisWorkbook.SaveAs ThisWorkbook.path & "/" & newName
        Kill oldName
    ElseIf Dir(ThisWorkbook.path & "/starkbank-sdk.xlsm") <> "" Then
        overwriteMessage = "O arquivo starkbank-sdk.xlsm será sobrescrito. Dados na planilha serão excluídos. Continuar?"
        overwriteLatest = MsgBox(overwriteMessage, vbYesNo)
    End If
    RenameCurrent = overwriteLatest
    
End Function

Public Sub DownloadLatest()
    Dim path As String: path = "https://github.com/starkbank/sdk-excel/raw/master/starkbank-sdk.xlsm"
    Dim targetFile As String: targetFile = TempFilePath()
    Dim oStream As Object
    Dim content As String
    Dim WinHttpReq As Object
    
    
    Set WinHttpReq = CreateObject("Msxml2.ServerXMLHTTP")
    WinHttpReq.Open "GET", path
    WinHttpReq.send
    
    If WinHttpReq.Status = 200 Then
        Set oStream = CreateObject("ADODB.Stream")
        oStream.Open
        oStream.Type = 1
        oStream.Write WinHttpReq.responseBody
        oStream.SaveToFile targetFile, 2
        oStream.Close
    End If
    
End Sub

Private Sub OpenDownloaded()
    Workbooks.Open ThisWorkbook.path & "/starkbank-sdk.xlsm"
    
End Sub

Private Sub CloseCurrent()
    ThisWorkbook.Close False
    
End Sub

Public Sub UpdateOld()
    Dim successMessage As String
    
    
    DownloadLatest
    UpdateModules
    successMessage = "Atualização concluída."
    MsgBox successMessage, , "Sucesso"
    
End Sub

Public Sub UpdateModules()
    Dim VBProjTo As VBIDE.vbProject
    Dim VBProjFrom As VBIDE.vbProject
    
    
    Set VBProjTo = ActiveWorkbook.vbProject
    path = Application.ActiveWorkbook.path
    fromname = TempFilePath()
    
    Workbooks.Open fromname
    Set VBProjFrom = ActiveWorkbook.vbProject
    
    For Each comp In VBProjFrom.VBComponents
        compType = ComponentTypeToString(comp.Type)
        CompName = comp.name
        validName = (CompName <> "Updater") And (CompName <> "JsonConverter") And (CompName <> "Response")
        If (compType = "Code Module" Or compType = "UserForm" Or compType = "Class Module") And validName Then
            Debug.Print "Copying to current sheet: " & comp.name
            Debug.Print CopyModule(comp.name, VBProjFrom, VBProjTo, True)
        End If
    Next
    
    ActiveWorkbook.Close False
    
End Sub

Private Function TempFilePath()
    TempFilePath = ThisWorkbook.path & "/" + "starkbank-sdk.xlsm"
    
End Function

Public Function getLatestRepoVersion()
    Dim path As String
    Dim query As String
    Dim getTags As Variant
    Dim resp As response
    
    
    path = baseRepoPath()
    query = "/tags"
    
    Set resp = getRepoRequest("api", path, query, New Dictionary)
    
    If resp.Status = 200 Then
        Set getTags = resp.json()
    Else
        MsgBox resp.error()("message"), , "Erro"
    End If
    
    getLatestRepoVersion = getTags(1)("name")
    
End Function

Public Function getLatestRepoFile()
    Dim path As String
    Dim query As String
    Dim content As String
    Dim getTags As Variant
    
    
    path = baseRepoPath()
    path = "/starkbank/sdk-excel"
    query = "/raw/master/SDK%20Stark%20Bank.xlsb.xlsm"
    
    Set resp = getRepoRequest("", path, query, New Dictionary)
    
    If resp.Status = 200 Then
        content = resp.content
    Else
        MsgBox resp.error()("message"), , "Erro"
    End If
    
    getLatestRepoFile = content
End Function


Public Function getCurrentVersion()
    getCurrentVersion = CStr(Sheets("Credentials").Cells(9, 2))
    
End Function

Public Function getRepoRequest(base As String, path As String, query As String, headers As Dictionary)
    Dim url As String: url = baseRepoUrl(base) + path + query
    
    
    Set getRepoRequest = updateFetch(url, "GET", headers, "")
    
End Function

Public Function updateFetch(url As String, method As String, headers As Dictionary, payload As String)
    Dim resp As response
    
    
    Set objHTTP = CreateObject("MSXML2.ServerXMLHTTP")
    objHTTP.Open method, url, False
    
    For Each key In headers.keys()
        objHTTP.setRequestHeader key, headers(key)
    Next
    
    objHTTP.send payload
    
    Set resp = New response
    
    resp.Status = objHTTP.Status
    resp.content = objHTTP.responseText
    
    Set updateFetch = resp

End Function

Public Function baseRepoUrl(repoString As String)
    Select Case repoString
        Case "api"
            baseRepoUrl = "https://api.github.com/repos"
        Case Else
            baseRepoUrl = "https://github.com"
    End Select
    
End Function

Public Function baseRepoPath()
    baseRepoPath = "/starkbank/sdk-excel"
    
End Function

Public Sub CopyTestModule()
    Dim moduleName As String
    Dim fromname As String
    Dim path As String
    Dim VBProjTo As VBIDE.vbProject
    Dim VBProjFrom As VBIDE.vbProject
    
    
    Set VBProjTo = ActiveWorkbook.vbProject
    path = Application.ActiveWorkbook.path
    fromname = TempFilePath()
    moduleName = "TestModule"
    
    Workbooks.Open fromname
    Set VBProjFrom = ActiveWorkbook.vbProject
    Debug.Print CopyModule(moduleName, VBProjFrom, VBProjTo, True)
    ActiveWorkbook.Close (False)
    ImportedTest moduleName
    
End Sub

Public Sub ImportedTest(moduleName As String)
    Dim zero As Integer
    
    
    zero = SimpleTest()
    ActiveWorkbook.vbProject.VBComponents.Remove ActiveWorkbook.vbProject.VBComponents(moduleName)
    
End Sub

Private Sub ListModules()
    Dim vbProj As VBIDE.vbProject
    Dim VBComp As VBIDE.VBComponent
    
    
    Set vbProj = ActiveWorkbook.vbProject
    
    For Each VBComp In vbProj.VBComponents
        Debug.Print VBComp.name, ComponentTypeToString(VBComp.Type)
    Next VBComp
    
End Sub

Private Function ComponentTypeToString(ComponentType As VBIDE.vbext_ComponentType) As String
    Select Case ComponentType
        Case vbext_ct_ActiveXDesigner
            ComponentTypeToString = "ActiveX Designer"
        Case vbext_ct_ClassModule
            ComponentTypeToString = "Class Module"
        Case vbext_ct_Document
            ComponentTypeToString = "Document Module"
        Case vbext_ct_MSForm
            ComponentTypeToString = "UserForm"
        Case vbext_ct_StdModule
            ComponentTypeToString = "Code Module"
        Case Else
            ComponentTypeToString = "Unknown Type: " & CStr(ComponentType)
    End Select
    
End Function
