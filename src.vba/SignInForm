
Private Sub ForgotPasswordButton_Click()
    Dim workspace As String: workspace = WorkspaceBox.Value
    Dim envString As String: envString = EnvironmentBox.Value
    Dim envDomain As String
    If envString = "" Then
        MsgBox "Preencha seu Ambiente (Sandbox ou Produção)!", vbExclamation
        Exit Sub
    End If
    If workspace = "" Then
        MsgBox "Preencha seu Workspace!", vbExclamation
        Exit Sub
    End If
    envDomain = IIf(LCase(envString) = "sandbox", "sandbox.", "")
    ActiveWorkbook.FollowHyperlink address:="https://" + envDomain + "web.starkbank.com/forgotPassword/" + workspace
    
End Sub

Private Sub ForgotWorkspaceButton_Click()
    Dim envString As String: envString = EnvironmentBox.Value
    Dim envDomain As String
    If envString = "" Then
        MsgBox "Preencha seu Ambiente (Sandbox ou Produção)!", vbExclamation
        Exit Sub
    End If
    envDomain = IIf(LCase(envString) = "sandbox", "sandbox.", "")
    ActiveWorkbook.FollowHyperlink address:="https://" + envDomain + "web.starkbank.com/signup/company/search"
    
End Sub

Private Sub UserForm_Initialize()
    Me.EnvironmentBox.AddItem "Produção"
    Me.EnvironmentBox.AddItem "Sandbox"
    
    Me.EmailBox.Value = SessionGateway.getEmail()
    Me.WorkspaceBox.Value = SessionGateway.getWorkspaceName()
    Me.EnvironmentBox.Value = SessionGateway.getEnvironmentString()
    
End Sub

Private Sub SendButton_Click()
    Dim workspace As String: workspace = WorkspaceBox.Value
    Dim email As String: email = EmailBox.Value
    Dim password As String: password = PasswordBox.Value
    Dim envString As String: envString = EnvironmentBox.Value
    Dim accessToken As String
    Dim memberName As String
    Dim workspaceId As String
    
    
    
    Call SessionGateway.saveSession(workspace, email, envString, "", "", "")
    Debug.Print "Session cleared"
    
    Set respJson = SessionGateway.createSession(email, password, workspace)
    Debug.Print "New session created"
    
    If respJson.exists("errors") Then
        If respJson("errors")(1)("code") = "invalidCredentials" Then
            MsgBox "Senha inválida."
            Unload Me
            Exit Sub
        End If
        MsgBox respJson("errors")(1)("message")
        Unload Me
        Exit Sub
    End If
    
    memberName = respJson("session")("userId")
    sessionId = respJson("session")("id")
    organizationId = respJson("session")("organizationId")
    
    Sheets("Credentials").Cells(13, 2) = "session/" + sessionId + "/workspace/" + workspace
    Set respJson = SessionGateway.getWorkspace(workspace)
    workspaceId = respJson("workspaces")(1)("id")
    
    Set respJson = SessionGateway.getPerson(email)
    memberName = respJson("person")("name")
    
    Call SessionGateway.saveSession(workspace, email, envString, CStr(sessionId), memberName, workspaceId)
    Debug.Print "New session data saved"
    
    Call SessionGateway.displayMemberInfo
    
    Unload Me
End Sub

