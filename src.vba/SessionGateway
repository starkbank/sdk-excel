
Public Function getBalance()
    Dim AccountInfo As Dictionary
    Dim balanceMessage As String
    
    
    Set AccountInfo = BankGateway.getAccount()
    balanceMessage = "-"
    
    If AccountInfo.Count > 0 Then
        Dim balance As Double
        balance = CDbl(AccountInfo("balances")(1)("amount"))
        balanceMessage = Utils.MoneyStringFrom(balance)
    End If
    
    getBalance = balanceMessage
    
End Function

Public Sub saveSession(workspace As String, email As String, envString As String, sessionId As String, memberName As String, workspaceId As String)
    Sheets("Credentials").Cells(1, 1) = "Workspace"
    Sheets("Credentials").Cells(1, 2) = workspace
    Sheets("Credentials").Cells(2, 1) = "E-mail"
    Sheets("Credentials").Cells(2, 2) = email
    Sheets("Credentials").Cells(3, 1) = "Environment"
    Sheets("Credentials").Cells(3, 2) = envString
    Sheets("Credentials").Cells(4, 1) = "SessionId"
    Sheets("Credentials").Cells(4, 2) = sessionId
    Sheets("Credentials").Cells(5, 1) = "Member.name"
    Sheets("Credentials").Cells(5, 2) = memberName
    Sheets("Credentials").Cells(6, 1) = "Workspace ID"
    Sheets("Credentials").Cells(6, 2) = workspaceId
    Sheets("Credentials").Cells(7, 1) = "Approval Date"
    
    Sheets("Credentials").Cells(11, 1) = "Session Private"
    Sheets("Credentials").Cells(12, 1) = "Session Public"
    Sheets("Credentials").Cells(13, 1) = "Access ID"
    
End Sub

Public Sub saveAccessToken(accessToken As String)
    Sheets("Credentials").Cells(4, 1) = "AccessToken"
    Sheets("Credentials").Cells(4, 2) = accessToken
    
End Sub

Public Sub displayMemberInfo()
    Dim helloMessage As String
    Dim workspaceMessage As String
    Dim emailMessage As String
    Dim envMessage As String
    Dim balanceMessage As String
    
    
    If Not isSignedin() Then
        MsgBox "Acesso negado, faça login novamente."
        Exit Sub
    End If
    
    helloMessage = "Olá " + SessionGateway.getMemberName() + "!"
    workspaceMessage = "Workspace: " + SessionGateway.getWorkspaceName()
    workspaceId = "ID do Workspace: " + SessionGateway.getWorkspaceId()
    emailMessage = "E-mail: " + SessionGateway.getEmail()
    envMessage = "Ambiente: " + SessionGateway.getEnvironmentString()
    balanceMessage = "Saldo: " + SessionGateway.getBalance()
    
    For Each WS In ThisWorkbook.Worksheets
        If WS.name <> "Credentials" And WS.name <> "InputLog" And WS.name <> "Aux" Then
            WS.Cells(2, 1).Value = helloMessage
            WS.Cells(3, 1).Value = workspaceMessage
            WS.Cells(4, 1).Value = workspaceId
            WS.Cells(5, 1).Value = emailMessage
            WS.Cells(6, 1).Value = envMessage
            WS.Cells(7, 1).Value = balanceMessage
        End If
    Next
End Sub

Public Function getSessionId()
    Dim sessionId As String: sessionId = Sheets("Credentials").Cells(4, 2)


    If sessionId = "" Then
        getSessionId = "Trash"
    Else
        getSessionId = sessionId
    End If
    
End Function

Public Function getEnvironment()
    Select Case getEnvironmentString()
        Case "Produção": getEnvironment = production
        Case "Sandbox":  getEnvironment = sandbox
    End Select
    
End Function

Public Function getEnvironmentString()
    getEnvironmentString = CStr(Sheets("Credentials").Cells(3, 2))
    
End Function

Public Function getWorkspaceName()
    getWorkspaceName = CStr(Sheets("Credentials").Cells(1, 2))
    
End Function

Public Function getEmail()
    getEmail = CStr(Sheets("Credentials").Cells(2, 2))
    
End Function

Public Function getMemberName()
    getMemberName = CStr(Sheets("Credentials").Cells(5, 2))
    
End Function

Public Function getWorkspaceId()
    getWorkspaceId = CStr(Sheets("Credentials").Cells(6, 2))
    
End Function

Public Function getSessionPrivateKeyContent() As String
    getSessionPrivateKeyContent = Sheets("Credentials").Cells(11, 2)
    
End Function

Public Function getSessionPublicKeyContent() As String
    getSessionPublicKeyContent = Sheets("Credentials").Cells(12, 2)
    
End Function

Public Function getAccessId() As String
    getAccessId = Sheets("Credentials").Cells(13, 2)
    
End Function

Public Sub RenewSessionKeys()
    Dim sessionPrivateKeySecret As String
    Dim sessionPublicKey As String
    
    
    sessionPrivateKeySecret = StarkBankEcdsa.generatePrivateKeySecret
    sessionPublicKey = StarkBankEcdsa.getPublicKeyFromSecret(sessionPrivateKeySecret)
    Sheets("Credentials").Cells(11, 2) = sessionPrivateKeySecret
    Sheets("Credentials").Cells(12, 2) = sessionPublicKey
    
End Sub

Public Sub DeleteSessionKeys()
    Sheets("Credentials").Cells(11, 2) = ""
    Sheets("Credentials").Cells(12, 2) = ""
    DeleteTempKeys
    
End Sub

Public Sub DeleteTempKeys()
    Dim sessionPrivateKeyPath
    Dim sessionPublicKeyPath
    sessionPrivateKeyPath = getTempDir() + "\" + "sessionPrivateKey.pem"
    sessionPublicKeyPath = getTempDir() + "\" + "sessionPublicKey.pem"
    Kill sessionPrivateKeyPath
    Kill sessionPublicKeyPath
    
End Sub

Public Function createSession(email As String, password As String, workspace As String)
    Dim sessionPublicKey As String
    Dim secret As String
    Dim uuid As String: uuid = "30cfd70f-a42b-49de-856d-b5cb93926a93"
    Dim memberPrivateKeySecret As String
    Dim memberPublicKey As String
    Dim resp As Response
    Dim dict As Dictionary
    Dim error As Dictionary
    Dim accessToken As String
    
    
    RenewSessionKeys
    sessionPublicKey = SessionGateway.getSessionPublicKeyContent()

    secret = encodeUri(uuid + ":" + email + ":" + password)
    memberPrivateKeySecret = StarkBankEcdsa.getSecretFromString(secret)
    memberPublicKey = StarkBankEcdsa.getPublicKeyFromSecret(memberPrivateKeySecret)
    
    Set resp = postSession(memberPrivateKeySecret, sessionPublicKey, email)
    
    If Not resp.Status = 200 Then
        Dim errors As Collection: Set errors = resp.errors()("errors")
        If errors(1)("code") = "missingCredentials" Then
            Set dict = getAccesToken(email, password, workspace)
            accessToken = dict("success")("accessToken")
            
            Set resp = postPublicKeyTransition(memberPublicKey, accessToken)
            Set resp = postSession(memberPrivateKeySecret, sessionPublicKey, email)
        End If
        
        
    End If
    
    Set createSession = resp.json()
    
End Function

Public Function postSession(memberPrivateKeySecret As String, sessionPublicKey As String, email As String)
    Dim payload As String
    Dim dict As New Dictionary
    Dim resp As Response
    Dim url As String: url = Rest.baseUrl() + "/v2-faks/session" 'TODO: remove -faks
    Dim headers As New Dictionary
    

    dict.Add "platform", "web"
    dict.Add "expiration", 5184000
    dict.Add "publicKey", sessionPublicKey

    payload = JsonConverter.ConvertToJson(dict)
    Set headers = sessionHeaders(payload, memberPrivateKeySecret, email)
    
    Set postSession = request.fetch(url, "POST", headers, payload)
    
End Function

Public Function postPublicKeyTransition(memberPublicKey As String, accessToken As String)
    Dim payload As String
    Dim dict As New Dictionary
    Dim resp As Response
    Dim url As String: url = Rest.baseUrl() + "/v1/auth/public-key-transition"
    Dim headers As New Dictionary
    
    
    dict.Add "publicKey", memberPublicKey
    payload = JsonConverter.ConvertToJson(dict)
    
    Set headers = defaultV1Headers(accessToken)
    Set postPublicKeyTransition = request.fetch(url, "POST", headers, payload)
    
End Function

Public Function getAccesToken(email As String, password As String, workspace As String)
    Dim resp As Response
    Dim payload As String
    Dim dict As New Dictionary
    Dim result As New Dictionary
    Dim headers As Dictionary
    Dim url As String: url = Rest.baseUrl() + "/v1/auth/access-token"
    
    
    dict.Add "email", email
    dict.Add "password", password
    dict.Add "platform", "web"
    dict.Add "workspace", workspace
    
    payload = JsonConverter.ConvertToJson(dict)
    Set headers = defaultV1Headers("")
    
    Set resp = request.fetch(url, "POST", headers, payload)

    If resp.Status = 200 Then
        result.Add "success", resp.json()
        result.Add "error", New Dictionary
    Else
        result.Add "success", New Dictionary
        result.Add "error", resp.error()
    End If
    
    Set getAccesToken = result
    
End Function

Public Function sessionHeaders(payload As String, memberPrivateKeySecret As String, email As String)
    Dim result As Dictionary
    Set result = New Dictionary
    Dim accessTime As Long
    Dim signature As String
    Dim currentVersion As String
    Dim message As String
    Dim accessId As String: accessId = "person/" + email
    
    
    accessTime = toUnix(Now)
    message = accessId + ":" + CStr(accessTime) + ":" + payload
    
    signature = StarkBankEcdsa.sign(message, memberPrivateKeySecret)

    result.Add "Content-Type", "application/json"
    result.Add "Accept-Language", "pt-BR"
    result.Add "Access-Time", accessTime
    result.Add "Access-Id", accessId
    result.Add "Access-Signature", signature
    result.Add "Platform-Id", "excel"
    result.Add "Platform-Version", Updater.getCurrentVersion()
    
    Set sessionHeaders = result
    
End Function

Public Function defaultV1Headers(accesToken As String)
    Dim result As Dictionary: Set result = New Dictionary
    
    
    result.Add "Content-Type", "Application/json"
    result.Add "Accept-Language", "pt-BR"
    If Not accesToken = "" Then
        result.Add "Access-Token", accesToken
    End If
    
    Set defaultV1Headers = result
    
End Function

Public Function getWorkspace(workspace As String)
    Dim query As String
    Dim resp As Response
    
    
    query = "?username=" + workspace
    payload = JsonConverter.ConvertToJson(dict)
    
    Set resp = Rest.getRequest("/v2-faks/workspace", query, New Dictionary)
    
    If resp.Status >= 300 Then
        MsgBox resp.errors()("errors")(1)("message"), , "Erro"
    End If
    
    Set getWorkspace = resp.json()
    
End Function

Public Function deleteSession()
    Dim path As String
    Dim accessId As String
    Dim sessionId As String
    Dim resp As Response

    
    sessionId = SessionGateway.getSessionId()
    Sheets("Credentials").Cells(13, 2) = "session/" + sessionId
    
    path = "/v2-faks/session/" + sessionId
    Set resp = Rest.deleteRequest(path, "", New Dictionary)
    
    If resp.Status <> 200 Then
        MsgBox resp.errors()("errors")(1)("message"), , "Erro"
    End If
    
    Set deleteSession = resp.json()
    
End Function

Public Function getPerson(email As String)
    Dim path As String
    Dim resp As Response
    
    
    path = "/v2-faks/person/" + email
    Set resp = Rest.getRequest(path, "", New Dictionary)
    
    If resp.Status >= 300 Then
        MsgBox resp.errors()("errors")(1)("message"), , "Erro"
    End If
    
    Set getPerson = resp.json()
    
End Function
