Public Function fetch(url As String, method As String, headers As Dictionary, payload As String)
    Set objHTTP = CreateObject("MSXML2.ServerXMLHTTP")
    objHTTP.Open method, url, False
    If DebugModeOn() Then
        DebugPrint "url", url
        DebugPrint "request", payload
    End If
    For Each key In headers.keys()
        objHTTP.setRequestHeader key, headers(key)
    Next
    
    Dim resp As response
    Set resp = New response
    On Error GoTo eh:
    objHTTP.send payload
    
    resp.Status = objHTTP.Status
    resp.content = objHTTP.responseText
    If DebugModeOn() Then
        DebugPrint "response", resp.content
        writeInputLog
    End If
    Set fetch = resp
    Exit Function
eh:
    resp.Status = 404
    resp.content = "{""errors"":[{""code"":""connectionError"",""message"":""Verifique sua conex√£o de internet!""}]}"
    Set fetch = resp
End Function

Public Function download(url As String, path As String, headers As Dictionary, fallbackName As String) As Boolean
    Set objHTTP = CreateObject("MSXML2.ServerXMLHTTP")
    
    If DebugModeOn() Then
        DebugPrint "url", url
    End If
    
    objHTTP.Open "GET", url, False
    Dim filepath As String
    Dim disposition As String
    
    For Each key In headers.keys()
        objHTTP.setRequestHeader key, headers(key)
    Next
    
    On Error GoTo eh:
    objHTTP.send
    If objHTTP.Status = 200 Then
        Set oStream = CreateObject("ADODB.Stream")
        oStream.Open
        oStream.Type = 1
        oStream.Write objHTTP.responseBody
        disposition = objHTTP.getResponseHeader("Content-Disposition")
        If disposition <> "" Then
            filename = Split(disposition, "filename=")(1)
        Else
            filename = fallbackName
        End If
        filepath = path + ReplaceFilenameChars(filename)
        oStream.SaveToFile filepath, 2 ' 1 = no overwrite, 2 = overwrite
        oStream.Close
        download = True
    Else
eh:
        If DebugModeOn() And Err.number <> 0 Then
            DebugPrint "error", "Error # " & str(Err.number)
            DebugPrint "errorMessage", Err.Source & Chr(13) & "Error Line: " & Erl & Chr(13) & Err.description
        ElseIf DebugModeOn() Then
            DebugPrint "response", objHTTP.responseText
        End If
        download = False
    End If
End Function
